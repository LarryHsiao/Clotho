buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        mavenCentral()
        jcenter()
        maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
}

apply plugin: 'java-library'
apply plugin: 'kotlin'

def clothoVersion = "1.16.2"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.23.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

afterEvaluate {
    test.dependsOn ktlintCheck
}

jar {
    from sourceSets.main.allSource
}

apply plugin: 'jacoco'
jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

apply plugin: 'com.jfrog.bintray'
bintray {
    user = 'larryhsiao'
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'Elizabeth'
        name = 'Clotho'
        userOrg = 'larryhsiao'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/LarryHsiao/Clotho.git'
        version {
            name = clothoVersion
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

apply plugin: 'maven-publish'
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.silverhetch'
            artifactId 'clotho'
            version clothoVersion
        }
    }
}

apply plugin: 'org.sonarqube'
sonarqube {
    properties {
        property "sonar.projectName", "Clotho"
        property "sonar.projectVersion", "${clothoVersion}"

        property 'sonar.sources', 'src/main/java'
        property "sonar.tests", "src/test/java"

//        property "sonar.java.binaries", "build/tmp/kotlin-classes/debug"

        property "sonar.jacoco.reportPaths", "build/jacoco/test.exec"
        property "sonar.junit.reportPahts", "build/test-results/test"
    }
}
