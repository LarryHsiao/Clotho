buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        mavenCentral()
        jcenter()
        maven { url = 'http://172.104.79.181:81/repository/Elizabeth/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
}

apply plugin: 'java-library'

def clothoVersion = "1.17.5"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.23.1'
    testRuntimeOnly 'com.h2database:h2:1.4.200'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 1.8
    }
}
afterEvaluate {
    test.dependsOn ktlintCheck
}

jar {
    from sourceSets.main.allSource
}

apply plugin: 'jacoco'
jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

apply plugin: 'com.jfrog.bintray'
bintray {
    user = 'larryhsiao'
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = 'Elizabeth'
        name = 'Clotho'
        userOrg = 'larryhsiao'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/LarryHsiao/Clotho.git'
        version {
            name = clothoVersion
            gpg {
                sign = true
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

apply plugin: 'maven-publish'
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.larryhsiao'
            artifactId 'clotho'
            version clothoVersion
        }
    }
}